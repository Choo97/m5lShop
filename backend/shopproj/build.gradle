plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.kosta'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.maven.apache.org/maven2' }
}

dependencies {
    // 1. Spring Boot 웹 & 데이터
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    // 2. 개발 툴 & 톰캣
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    
    // 3. DB & JSP
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    implementation 'javax.servlet:jstl'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'

    // 4. 유틸리티
    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.modelmapper:modelmapper:3.2.0'

    // 5. Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    
    // 6. 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

 	// 7. Security & OAuth2 & JWT
    implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'com.auth0:java-jwt:3.19.2'	
	
    implementation "com.querydsl:querydsl-jpa:5.0.0"
    
    // QueryDSL QClass 생성기 (apt 대신 annotationProcessor 사용)
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jpa"
    
    // java.lang.NoClassDefFoundError 에러 방지용
    annotationProcessor "javax.annotation:javax.annotation-api:1.3.2"
    annotationProcessor "javax.persistence:javax.persistence-api:2.2"
    
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootRun') {
    systemProperty 'file.encoding', 'UTF-8'
}

// ===================================================
// ★ QueryDSL 빌드 옵션 (Gradle 8.x 호환 방식) ★
// ===================================================
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDir querydslDir
}

tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean {
    delete file(querydslDir)
}